/*
 * Copyright (c) 2017. Nuvolect LLC
 *
 * This program is free software: you can redistribute it and/or modify it under the terms of the GNU
 * General Public License as published by the Free Software Foundation, either version 3 of the License,
 * or (at your option) any later version.
 *
 * Contact legal@nuvolect.com for a less restrictive commercial license if you would like to use the
 * software without the GPLv3 restrictions.
 *
 * This program is distributed in the hope that it will be useful, but WITHOUT ANY WARRANTY; without
 * even the implied warranty of MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License along with this program.  If not,
 * see <http://www.gnu.org/licenses/>.
 *
 */

apply plugin: 'com.android.application'

def versionMajor = 1
def versionMinor = 4
def versionPatch = 5
def versionBuild = 2

// Load keystore
def keystoreProperties = new Properties()
def keystorePropertiesPath = System.getProperty("user.home")+"/.gradle/keystore.properties"
    try {
        keystoreProperties.load(new FileInputStream(keystorePropertiesPath))
    } catch (FileNotFoundException exception) {
        keystoreProperties.setProperty('SS_keyAlias','dummy')
        keystoreProperties.setProperty('SS_storeFile','dummy')
        keystoreProperties.setProperty('SS_storePassword','dummy')
        keystoreProperties.setProperty('SS_keyPassword','dummy')
    }

android {
    signingConfigs {
        release {
            keyAlias keystoreProperties['SS_keyAlias']
            storeFile file(keystoreProperties['SS_storeFile'])
            storePassword keystoreProperties['SS_storePassword']
            keyPassword keystoreProperties['SS_keyPassword']
        }
        debug {
        }
    }

    compileSdkVersion 28
    buildToolsVersion "28.0.3"

    defaultConfig {
        applicationId "com.nuvolect.securesuite"
        minSdkVersion 21
        targetSdkVersion 28

        versionCode versionMajor * 10000 + versionMinor * 1000 + versionPatch * 100 + versionBuild
        versionName "${versionMajor}.${versionMinor}.${versionPatch}"

        testInstrumentationRunner 'android.support.test.runner.AndroidJUnitRunner'
    }

    buildTypes {
        debug {
            debuggable true
            applicationIdSuffix ".debug"
            versionNameSuffix "-debug"
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.debug
        }
        release {
            debuggable false
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
            signingConfig signingConfigs.release
        }
    }
    lintOptions {
        abortOnError false
    }
}

dependencies {
    implementation 'com.google.guava:guava:20.0'
    testImplementation 'com.google.guava:guava:20.0'
    implementation files('libs/commons-codec.jar')
    implementation files('libs/ez-vcard-0.9.6.jar')
    implementation files('libs/jackson-core-2.3.3.jar')
    implementation 'com.squareup.okio:okio:1.6.0'
    implementation 'com.squareup.okhttp:okhttp:2.6.0'
    implementation 'com.squareup.okhttp:okhttp-urlconnection:2.6.0'
    implementation 'net.zetetic:android-database-sqlcipher:3.5.4'
    implementation 'info.guardianproject.iocipher:IOCipher:0.4'
    implementation 'com.android.support:support-v4:28.0.0'
    implementation 'com.googlecode.libphonenumber:libphonenumber:7.1.0'
    implementation 'joda-time:joda-time:2.9.9'
    implementation 'com.google.code.gson:gson:2.8.2'

    // APACHE COMMONS LIBRARIES
    implementation 'commons-cli:commons-cli:1.3.1'
    implementation 'commons-io:commons-io:2.5'
    implementation group: 'org.apache.commons', name: 'commons-lang3', version: '3.4'

    // Required for local unit tests (JUnit 4 framework)
    testImplementation 'junit:junit:4.12'

    // Required for instrumented tests
    testImplementation 'com.android.support:support-annotations:28.0.0'
    testImplementation 'com.android.support.test:runner:1.0.2'
    testImplementation 'com.android.support.test:rules:1.0.2'
    testImplementation 'org.testng:testng:6.9.6'
}
