/*
 * Copyright (c) 2017. Nuvolect LLC
 *
 * This program is free software: you can redistribute it and/or modify it under the terms of the GNU
 * General Public License as published by the Free Software Foundation, either version 3 of the License,
 * or (at your option) any later version.
 *
 * Contact legal@nuvolect.com for a less restrictive commercial license if you would like to use the
 * software without the GPLv3 restrictions.
 *
 * This program is distributed in the hope that it will be useful, but WITHOUT ANY WARRANTY; without
 * even the implied warranty of MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License along with this program.  If not,
 * see <http://www.gnu.org/licenses/>.
 *
 */

apply plugin: 'com.android.application'

def versionMajor = 1
def versionMinor = 4
def versionPatch = 3
def versionBuild = 1

// Load keystore
def keystoreProperties = new Properties()
def keystorePropertiesPath = System.getProperty("user.home")+"/.gradle/keystore.properties"
    try {
        keystoreProperties.load(new FileInputStream(keystorePropertiesPath))
    } catch (FileNotFoundException exception) {
        keystoreProperties.setProperty('SS_keyAlias','dummy')
        keystoreProperties.setProperty('SS_storeFile','dummy')
        keystoreProperties.setProperty('SS_storePassword','dummy')
        keystoreProperties.setProperty('SS_keyPassword','dummy')
    }

android {
    signingConfigs {
        release {
            keyAlias keystoreProperties['SS_keyAlias']
            storeFile file(keystoreProperties['SS_storeFile'])
            storePassword keystoreProperties['SS_storePassword']
            keyPassword keystoreProperties['SS_keyPassword']
        }
        debug {
        }
    }

    compileSdkVersion 25
    buildToolsVersion "25.0.2"

    defaultConfig {
        applicationId "com.nuvolect.securesuite"
        minSdkVersion 19
        targetSdkVersion 24

        versionCode versionMajor * 10000 + versionMinor * 1000 + versionPatch * 100 + versionBuild
        versionName "${versionMajor}.${versionMinor}.${versionPatch}"

        testInstrumentationRunner 'android.support.test.runner.AndroidJUnitRunner'
    }

    buildTypes {
        debug {
            debuggable true
            applicationIdSuffix ".debug"
            versionNameSuffix "-debug"
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.debug
        }
        release {
            debuggable false
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
            signingConfig signingConfigs.release
        }
    }
    lintOptions {
        abortOnError false
    }
}

dependencies {
    compile 'com.google.guava:guava:20.0'
    compile 'org.immutables:gson:2.0.11'
    compile files('libs/commons-codec.jar')
    compile files('libs/ez-vcard-0.9.6.jar')
    compile files('libs/jackson-core-2.3.3.jar')
    compile 'com.squareup.okio:okio:1.3.0'
    compile 'com.squareup.okhttp:okhttp:2.3.0'
    compile 'com.squareup.okhttp:okhttp-urlconnection:2.3.0'
    compile 'net.zetetic:android-database-sqlcipher:3.5.4'
    compile 'info.guardianproject.iocipher:IOCipher:0.4'
    compile 'com.android.support:support-v4:25.3.1'
    compile 'com.googlecode.libphonenumber:libphonenumber:5.5'
    compile 'joda-time:joda-time:2.9.9'
    compile 'com.google.code.gson:gson:2.7'

    // APACHE COMMONS LIBRARIES
    compile 'commons-cli:commons-cli:1.3.1'
    compile 'commons-io:commons-io:2.5'
    compile group: 'org.apache.commons', name: 'commons-lang3', version: '3.0'

    // Required for local unit tests (JUnit 4 framework)
    testCompile 'junit:junit:4.12'

    // Required for instrumented tests
    androidTestCompile 'com.android.support:support-annotations:25.3.1'
    androidTestCompile 'com.android.support.test:runner:0.5'
    androidTestCompile 'com.android.support.test:rules:0.5'
}
